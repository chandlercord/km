/*
 * Copyright 2021 Kontain Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
/*
 * An ld script to be included using --script=km_guest_ldcmd
 * when km is linked.
 * It aligns the km guest code built into km on a 4k boundary.
 * The data is also 4k aligned.
 * The last page should be padded out to a 4k alignment.
 * And, finally these sections are placed at the very end of km
 * after the .ldata section.
 */
SECTIONS
{
   . = 0x8000000000;
   .init : { *(.init) }
   .plt : { *(.plt) }
   .fini : { *(.fini) }
   .text : { *(.text*) }
   .__libc_freeres_fn : { *(.__libc_freeres_fn) }
   .fini : { *(.fini) }
   .rodata : { *(.rodata) }
   .stapsdt.base : { *(.stapsdt.base) }
   .eh_frame : { *(.eh_frame) }
   .gcc_except_table : { *(.gcc_except_table) }
   .tdata : { *(.tdata) }
   .tbss : { *(.tbss) }
   .init_array : { *(.init_array) }
   .fini_array : { *(.fini_array) }
   .data.rel.ro : { *(.data.rel.ro) }
   .got : { *(.got) }
   .got.plt : { *(.got.plt) }
   .data : { *(.data*) }
   .__libc_subfreeres : { *(.__libc_subfreeres) }
   .__libc_IO_vtables : { *(.__libc_IO_vtables) }
   .__libc_atexit : { *(.__libc_atexit) }
   .bss : { *(.bss*) }
   .__libc_freeres_fn_section : { *(.__libc_freeres_fn_section) }
   .__libc_freeres_fn_section : { *(.__libc_freeres_fn_section) }

   .km_guest_text ALIGN(CONSTANT (COMMONPAGESIZE)) :
   {
      km_guest_start = .;
      km_guest_text_start = .;
      *(.km_guest_text)
   }
   .km_guest_data ALIGN(CONSTANT (COMMONPAGESIZE)) :
   {
      km_guest_data_start = .;
      *(.km_guest_data)
   }
   .km_guest_data_rw ALIGN(CONSTANT (COMMONPAGESIZE)) :
   {
      km_guest_data_rw_start = .;
      *(.km_guest_data_rw)
   }
   . = ALIGN(CONSTANT (COMMONPAGESIZE));
   km_guest_end = .;
}
INSERT AFTER .ldata
